2a - basic unsigned math
========================

### `++add`

Add

Produces the sum of `a` and `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

An atom.

Source
------

    ++  add                                                 ::  add
      ~/  %add
      |=  [a=@ b=@]
      ^-  @
      ?:  =(0 a)  b
      $(a (dec a), b +(b))

Examples
--------

    ~zod/try=> (add 2 2)
    4
    ~zod/try=> (add 1 1.000.000)
    1.000.001
    ~zod/try=> (add 1.333 (mul 2 2))
    1.337

------------------------------------------------------------------------

### `++cap`

Tree head

Tests whether an `a` is in the head or tail of a [noun](). Produces the
[cube]() `%2` if it is within the head, or the cube `%3` if it is
within the tail.

Accepts
-------

`a` is an atom.

Produces
--------

A [cube]().

Source
------

    ++  cap                                                 ::  tree head
      ~/  %cap
      |=  a=@
      ^-  ?(%2 %3)
      ?-  a
        %2        %2
        %3        %3
        ?(%0 %1)  !!
        *         $(a (div a 2))
      ==
    ::

Examples
--------

    ~zod/try=> (cap 4)
    %2
    ~zod/try=> (cap 6)
    %3
    ~zod/try=> (cap (add 10 9))
    %2

------------------------------------------------------------------------

### `++dec`

Decrement

Decrements `a` by `1`.

Accepts
-------

`a` is an [atom]().

Produces
--------

An atom.

Source
------

    ++  dec                                                 ::  decrement
      ~/  %dec
      |=  a=@
      ~|  %decrement-underflow
      ?<  =(0 a)
      =+  b=0
      |-  ^-  @
      ?:  =(a +(b))  b
      $(b +(b))
    ::

Examples
--------

    ~zod/try=> (dec 7)
    6
    ~zod/try=> (dec 0)
    ! decrement-underflow
    ! exit

------------------------------------------------------------------------

### `++div`

Divide

Computes `a` divided by `b`.

Accepts
-------

`a` is an [atom]().

`b` is an [atom]().

Produces
--------

An atom.

Source
------

    ++  div                                                 ::  divide
      ~/  %div
      |=  [a=@ b=@]
      ^-  @
      ~|  'div'
      ?<  =(0 b)
      =+  c=0
      |-
      ?:  (lth a b)  c
      $(a (sub a b), c +(c))
    ::

Examples
--------

    ~zod/try=> (div 4 2)
    2
    ~zod/try=> (div 17 8)
    2
    ~zod/try=> (div 20 30)
    0

------------------------------------------------------------------------

### `++fac`

Factorial

Computes the factorial of `a`.

Accepts
-------

`a` is an [atom]().

Produces
--------

An atom.

Source
------

    ++  fac                                                 ::  factorial
      ~/  %fac
      |=  a=@
      ^-  @
      ?:  =(0 a)  1
      (mul a $(a (dec a)))
    ::

Examples
--------

    ~zod/try=> (fac 3)
    6
    ~zod/try=> (fac 0)
    1
    ~zod/try=> (fac 11)
    39.916.800

------------------------------------------------------------------------

### `++gte`

Greater-than/equal

Tests whether `a` is greater than a number `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

A boolean.

Source
------

    ++  gte                                                 ::  greater-equal
      ~/  %gte
      |=  [a=@ b=@]
      ^-  ?
      !(lth a b)
    ::

Examples
--------

    ~zod/try=> (gte 100 10)
    %.y
    ~zod/try=> (gte 4 4)
    %.y
    ~zod/try=> (gte 3 4)
    %.n

------------------------------------------------------------------------

### `++gth`

Greater-than

Tests whether `a` is greater than `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

A boolean.

Source
------

    ++  gth                                                 ::  greater-than
      ~/  %gth
      |=  [a=@ b=@]
      ^-  ?
      !(lte a b)
    ::

Examples
--------

    ~zod/try=> (gth 'd' 'c')
    %.y
    ~zod/try=> (gth ~h1 ~m61)
    %.n

------------------------------------------------------------------------

### `++lte`

Less-than/equal

Tests whether `a` is less than or equal to `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

A boolean.

Source
------

    ++  lte                                                 ::  less-equal
      ~/  %lte
      |=  [a=@ b=@]
      |(=(a b) (lth a b))
    ::

Examples
--------

    ~zod/try=> (lte 4 5)
    %.y
    ~zod/try=> (lte 5 4)
    %.n
    ~zod/try=> (lte 5 5)
    %.y
    ~zod/try=> (lte 0 0)
    %.y

------------------------------------------------------------------------

### `++lth`

Less-than

Tests whether `a` is less than `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

A boolean.

Source
------

    ++  lth                                                 ::  less-than
      ~/  %lth
      |=  [a=@ b=@]
      ^-  ?
      ?&  !=(a b) 
          |-  
          ?|  =(0 a)  
              ?&  !=(0 b) 
                  $(a (dec a), b (dec b))
      ==  ==  ==
    ::

Examples
--------

    ~zod/try=> (lth 4 5)
    %.y
    ~zod/try=> (lth 5 4)
    %.n
    ~zod/try=> (lth 5 5)
    %.n
    ~zod/try=> (lth 5 0)
    %.n

------------------------------------------------------------------------

### `++mas`

Axis within head/tail?

Computes the axis of `a` within either the head or tail of a noun (depends whether `a` lies within the the head or tail).

Accepts
-------

`a` is an [atom]().

Produces
--------

An atom.

Source
------

    ++  mas                                                 ::  tree body
      ~/  %mas
      |=  a=@
      ^-  @
      ?-  a
        1   !!
        2   1
        3   1
        *   (add (mod a 2) (mul $(a (div a 2)) 2))
      ==
    ::

Examples
--------

    ~zod/try=> (mas 3)
    1
    ~zod/try=> (mas 4)
    2
    ~zod/try=> (mas 5)
    3
    ~zod/try=> (mas 6)
    2
    ~zod/try=> (mas 0)
    ! exit
    ~zod/try=> (mas 1)
    ! exit

-----------------------------------------------------------------------

### `++max`

Maximum

Computes the greater of `a` and `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

An atom.

Source
------

    ++  max                                                 ::  maximum
      ~/  %max
      |=  [a=@ b=@]
      ^-  @
      ?:  (gth a b)  a
      b
    ::

Examples
--------

    ~zod/try=> (max 10 100)
    100
    ~zod/try=> (max 10.443 9)
    10.443
    ~zod/try=> (max 0 1)
    1

------------------------------------------------------------------------

### `++min`

Minimum

Computes the lesser of `a` and `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

An atom.

Source
------

    ++  min                                                 ::  minimum
      ~/  %min
      |=  [a=@ b=@]
      ^-  @
      ?:  (lth a b)  a
      b
    ::

Examples
--------

    ~zod/try=> (min 10 100)
    10
    ~zod/try=> (min 10.443 9)
    9
    ~zod/try=> (min 0 1)
    0

------------------------------------------------------------------------

### `++mod`

Modulus

Computes the remainder of dividing `a` by `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

An atom.

Source
------

    ++  mod                                                 ::  remainder
      ~/  %mod
      |=  [a=@ b=@]
      ^-  @
      ?<  =(0 b)
      (sub a (mul b (div a b)))
    ::

Examples
--------

    ~zod/try=> (mod 5 2)
    1
    ~zod/try=> (mod 5 5)
    0

------------------------------------------------------------------------

### `++mul`

Multiply

Multiplies `a` by `b`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

An atom.

Source
------

    ++  mul                                                 ::  multiply
      ~/  %mul
      |=  [a=@ b=@]
      ^-  @
      =+  c=0
      |-
      ?:  =(0 a)  c
      $(a (dec a), c (add b c))
    ::

Examples
--------

    ~zod/try=> (mul 3 4)
     12 
    ~zod/try=> (mul 0 1) 
    0

------------------------------------------------------------------------

### `++peg`

Axis within axis

Computes the axis of `b` within axis `a`.

Accepts
-------

`a` is an [atom]().

`b` is an atom.

Produces
--------

An atom.

Source
------

    ++  peg                                                 ::  tree connect
      ~/  %peg
      |=  [a=@ b=@]
      ^-  @
      ?-  b
        1   a
        2   (mul a 2)
        3   +((mul a 2))
        *   (add (mod b 2) (mul $(b (div b 2)) 2))
      ==
    ::

Examples
--------

    ~zod/try=>  (peg 4 1)
    4
    ~zod/try=>  (peg 4 2)
    8
    ~zod/try=>  (peg 8 45)
    269

------------------------------------------------------------------------

### `++sub`

Subtract

Subtracts `b` from `a`.

Accepts
-------

`a` is an [atom]().

`b` is an [atom]().

Produces
--------

An atom.

Source
------

    ++  sub                                                 ::  subtract
      ~/  %sub
      |=  [a=@ b=@]
      ~|  %subtract-underflow
      ^-  @
      ?:  =(0 b)  a
      $(a (dec a), b (dec b))

Examples
--------

    ~zod/try=> (sub 10 5)
    5
    ~zod/try=> (sub 243 44)
    199
    ~zod/try=> (sub 5 0)
    5
    ~zod/try=> (sub 0 5)
    ! subtract-underflow
    ! exit

------------------------------------------------------------------------
